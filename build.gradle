buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'todoapi01'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Integration Tests via declaring a "source set".  Sets up relevant "configurations".
// See: https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
// 
// Java plugin will automagically set up a 'compileIntegrationTestJava' task that will compile source files
// under `src/integrationTest/java`.
// See: https://docs.gradle.org/current/userguide/java_plugin.html#source_sets
sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    // https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_plugin_and_dependency_management
    // `integrationTest` compile-time deps include `test` compile-time deps
    // Note: "implmentation" config is the new name for the "compile" config
    integrationTestImplementation.extendsFrom testImplementation
    // `integrationTest` runtime deps include `test` runtime deps
    integrationTestRuntime.extendsFrom runtime
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jdbc")

    runtime("mysql:mysql-connector-java:8.0.17")

    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testRuntime("com.h2database:h2")

}

// Define a task to run Integration Tests.  This is not created automatically.
// See: https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests - Ex. 15
task integrationTest(type: Test) {
    description = "Runs integration tests."
    group = "verification"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    // If configuring "gradle check" to run both tests and integration tests, we might want to run these after test
    shouldRunAfter test

    // Tests need connection to an external database.  See README.md (Running Integration Tests) on how to set up.
    def SPRING_DATASOURCE_URL=System.getenv("SPRING_DATASOURCE_URL")
    // Tell Spring about the external database.
    // See: https://docs.spring.io/spring-boot/docs/2.1.7.RELEASE/reference/html/boot-features-sql.html#boot-features-connect-to-production-database
    jvmArgs += "-Dspring.datasource.url=${SPRING_DATASOURCE_URL}"
}

// If we want "gradle check" to run both "test" and "integrationTest" tasks -- we don't, because check is run
// as part of build, and we don't want to run integration tests at that time.
// check.dependsOn integrationTest
